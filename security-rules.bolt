
isUser(uid) { auth != null && auth.uid == uid }
isEitherUser(uid1, uid2) { auth != null && (auth.uid == uid1 || auth.uid == uid2) }

path /v0/users {
	read() { auth != null }
}

path /v0/users/{uid} is User {
	write() { isUser(uid) }
	validate() { newData.child("uid").val() == uid }
}

path /v0/user-contacts/{uid} is Object {
	read() { isUser(uid) }
	write() { isUser(uid) }
}

path /v0/user-contacts/{uid}/{contact_uid} is UserContact {
	read() { isEitherUser(uid, contact_uid) }
	write() { isUser(uid) }
}

path /v0/user-contacts/{uid}/{contact_uid}/accepted {
	read() { isEitherUser(uid, contact_uid) }
	write() { isUser(contact_uid) || (isUser(uid) && !newData.val()) }
}

path /v0/contact-requests/{contact_uid}/{from_uid} is Boolean {
	read() { isEitherUser(from_uid, contact_uid) }
	write() { (isUser(contact_uid) && newData.val()) || (isEitherUser(from_uid, contact_uid) && !newData.val()) }
}

type UserContact {
	accepted: Boolean
}

type User {
	uid: String
	name: String
	photoURL: String | Null
}
